%  http://hackingoff.com/compilers/ll-1-parser-generator


PROGRAM -> 'program' 'id' ';' DECL BLOCK '.'

DECL -> CONSTS USERTYPES VARS SUBPROGRAMS

CONSTS -> LAMBDA
CONSTS -> 'const' LISTCONST

LISTCONST -> CONSTDECL LISTCONSTPRIME

LISTCONSTPRIME -> LAMBDA
LISTCONSTPRIME -> LISTCONST

CONSTDECL -> 'id' '=' EXPR ';'

TYPES -> 'id' TYPESPRIME
TYPES -> PRIMTYPES

TYPESPRIME -> LAMBDA
TYPESPRIME -> '..' SUBRANGETYPE

PRIMTYPES -> 'int' PRIMTYPESPRIME
PRIMTYPES -> 'real'
PRIMTYPES -> 'bool'
PRIMTYPES -> 'char' PRIMTYPESPRIME
PRIMTYPES -> 'string'
PRIMTYPES -> ARRAYTYPE
PRIMTYPES -> SETTYPE
PRIMTYPES -> ENUMTYPE
PRIMTYPES -> RECORDTYPE

PRIMTYPESPRIME -> '..' SUBRANGETYPE
PRIMTYPESPRIME -> LAMBDA

ARRAYTYPE -> 'array' '[' SUBRANGELIST ']' 'of' TYPES

SUBRANGELIST -> 'id' '..' SUBRANGETYPE SUBRANGELISTPRIME
SUBRANGELIST -> 'int' '..' SUBRANGETYPE SUBRANGELISTPRIME
SUBRANGELIST -> 'char' '..' SUBRANGETYPE SUBRANGELISTPRIME

SUBRANGELISTPRIME -> LAMBDA
SUBRANGELISTPRIME -> ',' SUBRANGELIST

SUBRANGETYPE -> 'id'
SUBRANGETYPE -> 'int'
SUBRANGETYPE -> 'char'

SETTYPE -> 'set' 'of' TYPES

ENUMTYPE -> '(' IDLIST ')'

RECORDTYPE -> 'record' VARLISTLIST 'end'

USERTYPES -> LAMBDA
USERTYPES -> 'type' LISTUSERTYPES

LISTUSERTYPES -> USERTYPE LISTUSERTYPESPRIME

LISTUSERTYPESPRIME -> LAMBDA
LISTUSERTYPESPRIME -> LISTUSERTYPES

USERTYPE -> 'id' '=' TYPES ';'

VARS -> LAMBDA
VARS -> 'var' VARLISTLIST

VARLISTLIST -> VARLIST VARLISTLISTPRIME

VARLISTLISTPRIME -> LAMBDA
VARLISTLISTPRIME -> VARLISTLIST

VARLIST -> TYPES IDLIST ';'

IDLIST -> 'id' IDATTR IDLISTPRIME

IDLISTPRIME -> LAMBDA
IDLISTPRIME -> ',' IDLIST

IDATTR -> LAMBDA
IDATTR -> '=' EXPR

VARIABLE -> '->' 'id' VARIABLEPRIME
VARIABLE -> '[' EXPRLIST+ ']' VARIABLEPRIME

VARIABLEPRIME -> VARIABLE
VARIABLEPRIME -> LAMBDA

BLOCK -> 'begin' STMTS 'end'

STMTS ->  STMT STMTLISTPRIME

STMTLISTPRIME -> LAMBDA
STMTLISTPRIME -> ';' STMTS

STMT -> LAMBDA
STMT -> 'label' STMT
STMT -> BLOCK
STMT -> WRITESTMT
STMT -> WRITELNSTMT
STMT -> READSTMT
STMT -> READLNSTMT
STMT -> LOOPBLOCK
STMT -> IFBLOCK
STMT -> FORBLOCK
STMT -> CASEBLOCK
STMT -> GOTOSTMT
STMT -> 'id' STMTPRIME
STMT -> EXITSTMT
STMT -> RETURNSTMT

STMTPRIME -> ATTRSTMT
STMTPRIME -> SUBPROGCALL

SUBPROGCALL -> '(' EXPRLIST ')'

EXISTSTMT -> 'exitwhen' '(' EXPR ')'

RETURNSTMT -> 'return' EXPR

ATTRSTMT -> 'id' ATTRSTMTPRIME

ATTRSTMTPRIME -> VARIABLE ':=' EXPR
ATTRSTMTPRIME -> ':=' EXPR

IFBLOCK -> 'if' '(' EXPR ')' STMT ELSEBLOCK

ELSEBLOCK -> LAMBDA
ELSEBLOCK -> 'else' STMT

LOOPBLOCK -> 'loop' STMT

CASEBLOCK -> 'case' EXPR 'of' CASELIST CASEBLOCKPRIME

CASEBLOCKPRIME -> 'end'
CASEBLOCKPRIME -> 'else' STMT 'end'

CASELIST -> LITERALLIST ':' STMT ';'

LITERALLIST -> LITERAL LITERALLISTPRIME

LISTERALLISTPRIME -> LAMBDA
LISTERALLISTPRIME -> ',' LITERALLIST

GOTOSTMT -> 'goto' 'label'

FORBLOCK -> 'for' 'id' FORBLOCKPRIME

FORBLOCKPRIME -> VARIABLE ':=' EXPR 'to' EXPR 'step' EXPR 'do' STMT
FORBLOCKPRIME -> ':=' EXPR 'to' EXPR 'step' EXPR 'do' STMT

EXPR -> CONJ DISJ

DISJ -> LAMBDA
DISJ -> '||' CONJ

FINAL-TERM -> 'id' FINAL-TERMPRIME
FINAL-TERM -> LITERAL
FINAL-TERM -> '(' EXPR ')'

FINAL-TERMPRIME -> VARIABLE
FINAL-TERMPRIME -> LAMBDA
FINAL-TERMPRIME -> SUBPROGCALL

ADD-OP -> '+'
ADD-OP -> '-'

MUL-OP -> '*'
MUL-OP -> '/'
MUL-OP -> '%'

EQUALITY-OP -> '=='
EQUALITY-OP -> '!='

RELATIONAL-OP -> '<'
RELATIONAL-OP -> '<='
RELATIONAL-OP -> '>'
RELATIONAL-OP -> '>='

CONJ -> COMP CONJPRIME

CONJPRIME -> LAMBDA
CONJPRIME -> '&&' COMP

COMP -> RELATIONAL COMPPRIME

COMPPRIME -> LAMBDA
COMPPRIME -> EQUALITY-OP RELATIONAL

RELATIONAL -> SUM RELATIONALPRIME

RELATIONALPRIME -> LAMBDA
RELATIONALPRIME -> RELATIONAL-OP SUM

SUM -> NEG SUMPRIME

SUMPRIME -> LAMBDA
SUMPRIME -> ADD-OP NEG SUMPRIME

NEG -> MUL
NEG -> '!' MUL

MUL -> FINAL-TERM MUL'

MUL' -> LAMBDA
MUL' -> MUL-OP FINAL-TERM MUL'

LITERAL -> 'intliteral'
LITERAL -> 'realliteral'
LITERAL -> 'charliteral'
LITERAL -> 'stringliteral'
LITERAL -> SUBRANGELITERAL

EXPRLIST -> LAMBDA
EXPRLIST -> EXPRLIST+

EXPRLIST+ -> EXPR EXPRLIST+PRIME

EXPRLIST+PRIME -> LAMBDA
EXPRLIST+PRIME -> ',' EXPRLIST+

SUBPROGRAMS -> LAMBDA
SUBPROGRAMS -> PROCEDURE SUBPROGRAMSPRIME
SUBPROGRAMS -> FUNCTION SUBPROGRAMSPRIME

SUBPROGRAMSPRIME -> LAMBDA
SUBPROGRAMSPRIME -> ';' SUBPROGRAMS

PROCEDURE -> 'proc' 'id' '(' PARAM ')' ';' DECL BLOCK

FUNCTION -> 'func' TYPES 'id' '(' PARAM ')' ';' DECL BLOCK

PARAM -> LAMBDA
PARAM -> VARLISTLIST

WRITESTMT -> 'write' '(' EXPR ')'

WRITELNSTMT -> 'writeln' '(' EXPR ')'

READSTMT -> 'read' '(' 'id' ')'

READLNSTMT -> 'readln' (' 'id' ')'
