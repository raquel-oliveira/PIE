% http://jsmachines.sourceforge.net/machines/ll1.html

PROGRAM -> 'program' 'id' ';' DECL BLOCK '.'

DECL -> CONSTS USERTYPES VARS SUBPROGRAMS

CONSTS -> ''
CONSTS -> 'const' LISTCONST

LISTCONST -> CONSTDECL LISTCONSTPRIME

LISTCONSTPRIME -> ''
LISTCONSTPRIME -> LISTCONST

CONSTDECL -> 'id' '=' EXPR ';'

TYPES -> 'id' TYPESPRIME
TYPES -> PRIMTYPES

TYPESPRIME -> ''
TYPESPRIME -> '..' SUBRANGETYPE

PRIMTYPES -> 'int' TYPESPRIME
PRIMTYPES -> 'real'
PRIMTYPES -> 'bool'
PRIMTYPES -> 'char' TYPESPRIME
PRIMTYPES -> 'string'
PRIMTYPES -> ARRAYTYPE
PRIMTYPES -> SETTYPE
PRIMTYPES -> ENUMTYPE
PRIMTYPES -> RECORDTYPE

ARRAYTYPE -> 'array' '[' SUBRANGELIST ']' 'of' TYPES

SUBRANGELIST -> 'id' SUBRANGEPRIME
SUBRANGELIST -> 'int' '..' SUBRANGETYPE SUBRANGELISTPRIME
SUBRANGELIST -> 'char' '..' SUBRANGETYPE SUBRANGELISTPRIME

SUBRANGEPRIME -> SUBRANGELISTPRIME
SUBRANGEPRIME -> '..' SUBRANGETYPE SUBRANGELISTPRIME

SUBRANGELISTPRIME -> ''
SUBRANGELISTPRIME -> ',' SUBRANGELIST

SUBRANGETYPE -> 'id'
SUBRANGETYPE -> 'int'
SUBRANGETYPE -> 'char'

SETTYPE -> 'set' 'of' TYPES

ENUMTYPE -> '(' IDLIST ')'

RECORDTYPE -> 'record' VARLISTLIST 'end'

USERTYPES -> ''
USERTYPES -> 'type' LISTUSERTYPES

LISTUSERTYPES -> USERTYPE LISTUSERTYPESPRIME

LISTUSERTYPESPRIME -> ''
LISTUSERTYPESPRIME -> LISTUSERTYPES

USERTYPE -> 'id' '=' TYPES ';'

VARS -> ''
VARS -> 'var' VARLISTLIST

VARLISTLIST -> VARLIST VARLISTLISTPRIME

VARLISTLISTPRIME -> ''
VARLISTLISTPRIME -> VARLISTLIST

VARLIST -> TYPES IDLIST ';'

IDLIST -> 'id' IDATTR IDLISTPRIME

IDLISTPRIME -> ''
IDLISTPRIME -> ',' IDLIST

IDATTR -> ''
IDATTR -> '=' EXPR

VARIABLE -> '=>' 'id' VARIABLEPRIME
VARIABLE -> '[' EXPRLISTPLUS ']' VARIABLEPRIME

VARIABLEPRIME -> VARIABLE
VARIABLEPRIME -> ''

BLOCK -> 'begin' STMTS 'end'

STMTS -> STMT STMTLISTPRIME

STMTLISTPRIME -> ''
STMTLISTPRIME -> ';' STMTS

STMT -> ''
STMT -> 'label' STMT
STMT -> BLOCK
STMT -> WRITESTMT
STMT -> WRITELNSTMT
STMT -> READSTMT
STMT -> READLNSTMT
STMT -> LOOPBLOCK
STMT -> IFBLOCK
STMT -> FORBLOCK
STMT -> CASEBLOCK
STMT -> GOTOSTMT
STMT -> 'id' STMTPRIME
STMT -> EXITSTMT
STMT -> RETURNSTMT

STMTPRIME -> ATTRSTMT
STMTPRIME -> SUBPROGCALL

SUBPROGCALL -> '(' EXPRLIST ')'

EXITSTMT -> 'exitwhen' EXPR

RETURNSTMT -> 'return' EXPR

ATTRSTMT -> VARIABLE ':=' EXPR
ATTRSTMT -> ':=' EXPR

IFBLOCK -> 'if' EXPR STMT ELSEBLOCK

ELSEBLOCK -> ''
ELSEBLOCK -> 'else' STMT

LOOPBLOCK -> 'loop' STMT

CASEBLOCK -> 'case' EXPR 'of' CASELIST CASEBLOCKPRIME

CASEBLOCKPRIME -> 'end'
CASEBLOCKPRIME -> 'else' STMT 'end'

CASELIST -> LITERALLIST ':' STMT ';'

LITERALLIST -> LITERAL LITERALLISTPRIME

LITERALLISTPRIME -> ''
LITERALLISTPRIME -> ',' LITERALLIST

GOTOSTMT -> 'goto' 'label'

FORBLOCK -> 'for' 'id' FORBLOCKPRIME

FORBLOCKPRIME -> VARIABLE ':=' EXPR 'to' EXPR 'step' EXPR 'do' STMT
FORBLOCKPRIME -> ':=' EXPR 'to' EXPR 'step' EXPR 'do' STMT

EXPR -> CONJ DISJ

FINAL_TERM -> 'id' FINAL_TERMPRIME
FINAL_TERM -> LITERAL
FINAL_TERM -> '(' EXPR ')'

FINAL_TERMPRIME -> VARIABLE
FINAL_TERMPRIME -> ''
FINAL_TERMPRIME -> SUBPROGCALL

DISJ -> ''
DISJ -> '||' CONJ

CONJ -> COMP CONJPRIME

CONJPRIME -> ''
CONJPRIME -> '&&' COMP

COMP -> RELATIONAL COMPPRIME

RELATIONAL -> SUM RELATIONALPRIME

RELATIONALPRIME -> ''
RELATIONALPRIME -> RELATIONAL_OP SUM

COMPPRIME -> ''
COMPPRIME -> EQUALITY_OP RELATIONAL

SUM -> NEG SUMPRIME

SUMPRIME -> ''
SUMPRIME -> ADD_OP NEG SUMPRIME

NEG -> MUL
NEG -> '!' MUL

MUL -> FINAL_TERM MULPRIME

MULPRIME -> ''
MULPRIME -> MUL_OP FINAL_TERM MULPRIME

ADD_OP -> '+'
ADD_OP -> '-'

MUL_OP -> '*'
MUL_OP -> '/'
MUL_OP -> '%'

EQUALITY_OP -> '=='
EQUALITY_OP -> '!='

RELATIONAL_OP -> '<'
RELATIONAL_OP -> '<='
RELATIONAL_OP -> '>'
RELATIONAL_OP -> '>='

LITERAL -> 'intliteral'
LITERAL -> 'realliteral'
LITERAL -> 'charliteral'
LITERAL -> 'stringliteral'
LITERAL -> 'subrangeliteral'

EXPRLIST -> ''
EXPRLIST -> EXPRLISTPLUS

EXPRLISTPLUS -> EXPR EXPRLISTPLUSPRIME

EXPRLISTPLUSPRIME -> ''
EXPRLISTPLUSPRIME -> ',' EXPRLISTPLUS

SUBPROGRAMS -> ''
SUBPROGRAMS -> PROCEDURE SUBPROGRAMSPRIME
SUBPROGRAMS -> FUNCTION SUBPROGRAMSPRIME

SUBPROGRAMSPRIME -> ''
SUBPROGRAMSPRIME -> ';' SUBPROGRAMS

PROCEDURE -> 'proc' 'id' '(' PARAM ')' ';' DECL BLOCK

FUNCTION -> 'func' TYPES 'id' '(' PARAM ')' ';' DECL BLOCK

PARAM -> ''
PARAM -> PARAMLISTLIST

PARAMLISTLIST -> PARAMLIST PARAMLISTLISTPRIME

PARAMLISTLISTPRIME -> ''
PARAMLISTLISTPRIME -> ';' PARAMLISTLIST

PARAMLIST -> 'ref' TYPES IDLIST
PARAMLIST -> TYPES IDLIST

WRITESTMT -> 'write' '(' EXPR ')'

WRITELNSTMT -> 'writeln' '(' EXPR ')'

READSTMT -> 'read' '(' 'id' VARIABLEPRIME ')'

READLNSTMT -> 'readln' (' 'id' VARIABLEPRIME ')'
